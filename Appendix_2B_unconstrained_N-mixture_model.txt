Appendix 2B: OpenBUGS code for the multi-year unconstrained N-mixture model

This model is similar to the CMR model but with no distinction between intial "captures" and "recaptures".
It was used to obtain unconstrained estimates of N for each transect in each of the three years sampled.
It was also used to obtain estimates from the 2021 data only for comparison with CMR estimates.
This involved adjusting the model so that only the 2021 data were sampled (see below).

Initial values: these need to be at or above the minimum number on each transect each year
list(N=c(0,29,38,17,20,10,7,0,12,14,3,3,0,1,1,30,35,54,12,16,13,4,2,26,11,3,0,1,3,0,29,28,30,19,8,28,1,1,7,24,9,2,0,1,2))

Model {

	# Detection model
	a.p ~ dnorm(0,1)		# intercept of logit of detection probability (p)
	s.j.p ~ dunif(0,1)		# standard deviation among surveys in logit p
	tau.j.p <- pow(s.j.p,-2)	# convert to precision

	for (j in 1:n.surveys.max) {		# for each survey...
		re.j.p[j] ~ dnorm(0, tau.j.p)		# assign nightly random effect on detection prob.
		logit(p[j]) <- a.p+re.j.p[j]		# calc. prob of individual frog being detected
	}

	# Abundance model
	for (i in 1:n.rows) {	# for each transect for each year... 
	  
		a.N[i] ~ dnorm(0,0.1)		# prior for expected value of log(N)
		log(mu[i]) <- a.N[i]		# convert to untransformed N
		N[i] ~ dpois(mu[i])		# sample actual of N from expected value for N				
		}

#	for (i in 31:n.rows) {	# for each transect in 2021 only... 
	for (i in 1:n.rows) {	# for each transect for each year... 
		for (j in 1:n.surveys[i]) {		# for each sampling occasion
			n[i,j] ~ dbin(p[j],N[i])		# sample number frogs found from number present
		}

	}
}